```{julia; eval=true; echo=false}
# using Gadfly
using PhyloNetworks
```
# Bootstrap

## Running a bootstrap analysis

There are two ways to do a bootstrap analysis.

- From quartet CFs with credibility intervals, such as if we used BUCKy. The [TICR pipeline](@ref) outpus a CF table with extra columns for credibility intervals. We could then read that table and get bootstrap networks like this, and tweak options as needed:
```julia
using CSV
df = CSV.read("tableCF_withCI.csv")
bootnet = bootsnaq(startnetwork, df, hmax=1, filename="bootstrap")
```

- Alternatively, we can use bootstrap gene trees: one file of bootstrap trees per gene. Here, the input is a text file that lists all the bootstrap files (one per gene). We demonstrate this option here.

The names of all our bootstrap files are listed in "BSlistfiles".
(ASTRAL can use the same file to do its own bootstrap, see the
[wiki](https://github.com/crsl4/PhyloNetworks.jl/wiki/Gene-Trees:-RAxML)
for more details).
The function `readBootstrapTrees` can read this list of file names, then
read each bootstrap file to get the bootstrap sample for each gene.
We can use them to sample input gene trees at random, one per gene,
and estimate a network from them. We ask the `bootsnaq` function
to repeat this resampling of bootstrap gene trees several times.

```{julia; eval=false}
bootTrees = readBootstrapTrees("BSlistfiles");
bootnet = bootsnaq(net0, bootTrees, hmax=1, nrep=10, runs=3, filename="bootsnaq", seed=4321)
```
```{julia; echo=false}
bootnet = readMultiTopology("bootsnaq.out");
```

The bootstrap networks are saved in the `boostrap.out` file, so they
can be read in a new session with
`bootnet = readMultiTopology("bootsnap.out")`. To save the bootstrap networks to
a different file (perhaps after having re-rooted them with an
outgroup), we could do this: `writeMultiTopology(bootnet, "bootstrapNets.tre")`.

The example above asks for 10 bootstrap replicates,
which is definitely too few, to make the example run faster.
We might also increase the number of optimization runs (`runs`)
done for each bootstrap replicate. This bootstrap was run with the
default 10 runs per replicate, and 100 bootstrap replicates,
and the 100 bootstrap networks come with the package:

```{julia; eval=true; results="markup"; term=true}
bootnet = readMultiTopology(Pkg.dir("PhyloNetworks","examples","bootsnaq.out"));
length(bootnet)
```

If we used a specified list of quartets on the original data, we
should use that same list for the bootstrap analysis through the
option `quartetfile`.

## support for tree edges

Now that we have 100 bootstrap networks, we need to summarize
what they have in common (highly supported features) and what they
don't (areas of uncertainty).

Before summarizing these bootstrap networks on the best network,
it is best to re-read this network to get a reproducible internal numbering
of its nodes and edges, used later for mapping bootstrap support to edges.
```{julia; eval=true}
net1 = readTopology(joinpath(Pkg.dir("PhyloNetworks"),"examples","net1.out"))
```

It turns out that the direction of gene flow is quite uncertain
in this example (see below) with a wrong direction inferred sometimes,
so we re-root our best network net1 to the base of O,E, for the figures
to be less confusing later.

```{julia; eval=true; echo=false}
rootonedge!(net1, 7)
```
```{julia; eval=false; results="markup"; label="net1_rotate1"; fig_width=4; fig_height=4}
using PhyloPlots
plot(net1, showEdgeNumber=true) # edge 7 leads to O+E
rootonedge!(net1, 7) # makes (O,E) outgroup clade
plot(net1, showNodeNumber=true)
```
![net1_rotate1 1](../assets/figures/bootstrap_net1_rotate1_1.png)
![net1_rotate1 2](../assets/figures/bootstrap_net1_rotate1_2.png)

edges cross: but rotating at node -6 should remove this crossing
of edges
```{julia; eval=true; results="markup"}
rotate!(net1, -6)
```
```{julia; eval=false; label="net1_rotate2"; fig_width=4; fig_height=4}
plot(net1, showGamma=true)
```
![net1_rotate2](../assets/figures/bootstrap_net1_rotate2_1.png)

We can now summarize our bootstrap networks.
The functions `treeEdgesBootstrap` and `hybridBootstrapSupport`
read all bootstrap networks and map the edges / nodes
onto a reference network: here net1.
```{julia; eval=true}
BSe_tree, tree1 = treeEdgesBootstrap(bootnet,net1)
```
This calculates the major tree `tree1` displayed in `net1`, that is,
the tree obtained by following the major parent (γ>0.5) of each hybrid node.
This tree can be visualized like this, with edge numbers shown for later use.
```{julia; eval=false; results="markup"; label="major_tree"; fig_width=4; fig_height=4}
plot(tree1, showEdgeNumber=true)
```
![major_tree](../assets/figures/bootstrap_major_tree_1.png)

Next, we can look at bootstrap table `BSe_tree`, which has one row for
each tree edge in `net1`. One column contains the edge number
(same as shown in the plot) and another column contains the edge
bootstrap support: the proportion of bootstrap replicates in which this edge was
found in the major tree of the inferred network.
We can see the full bootstrap table and see
which tree edges have bootstrap support lower than 100% (none here) with
```{julia; eval=true; term=true; results="markup"}
showall(BSe_tree)
BSe_tree[BSe_tree[:proportion] .< 100.0, :]
```
Finally, we can map the bootstrap proportions onto the network or its main tree
by passing the bootstrap table to the `edgeLabel` option of `plot`:
```{julia; eval=false; results="markup"; label="boot_tree_net"; fig_width=4; fig_height=4}
plot(tree1, edgeLabel=BSe_tree)
plot(net1,  edgeLabel=BSe_tree)
```
![boot_tree_net 1](../assets/figures/bootstrap_boot_tree_net_1.png)
![boot_tree_net 2](../assets/figures/bootstrap_boot_tree_net_2.png)

(Here, it is important that the numbers assigned to edges when building the boostrap
table --those in `net1` at the time-- correspond to the current edge numbers
in `tree1` and `net1`. That was the purpose of reading the network from the
output file of `snaq!` earlier, for consistency across different Julia sessions.)

If we wanted to plot only certain bootstrap values, like those below 100% (1.0),
we could do this:
```{julia; eval=false; results="markup"; label="boot_net"; fig_width=4; fig_height=4}
plot(net1, edgeLabel=BSe_tree[BSe_tree[:proportion] .< 100.0, :])
```

## support for hybrid edges and hybrid nodes

Summarizing the placement of reticulations is not standard.
The function `hybridBootstrapSupport` attempts to do so.
The descendants of a given hybrid node form the "recipient" or "hybrid" clade,
and is obtained after removing all other reticulations.
If reticulation is due to gene flow or introgression, the minor hybrid edge (with γ<0.5)
represents this event. The descendants of the lineage from which gene flow originated
is then a second "sister" of the hybrid clade. Because of the reticulation event,
the hybrid clade has 2 sister clades, not 1: the major sister (through the major hybrid edge
with γ>0.5) and the minor sister (through the minor hybrid edge with γ<0.5).
Note that the network says *nothing* about the process: its shows the *relationships* only.
We can calculate the frequency that each clade is a hybrid clade, or a major or minor sister
for some other hybrid, in the bootstrap networks:
```{julia; eval=true}
BSn, BSe, BSc, BSgam, BSedgenum = hybridBootstrapSupport(bootnet, net1);
```
Let's look at the results.
We can list all the clades and the percentage of bootstrap networks (bootstrap support)
in which each clade is a hybrid or sister to a hybrid:
```{julia; eval=true; term=true; results="markup"}
BSn
```
If a clade contains a single taxon, it is listed with its taxon name.
The clade found in the best network is listed with its tag, starting with H (e.g. "H7").
The name of other clades start with "c_" followed by their number in the best network, if they
do appear in the best network.
The node numbers, as used internally in the best network, are listed in a separate column.
They can be used later to display the bootstrap support values onto the network.
Various columns give the bootstrap support that each clade is a hybrid, or a (major/minor) sister
to a hybrid. The last column gives the bootstrap support for the full relationship in the
best network: same hybrid with same two sisters.
These bootstrap values are associated with nodes (or possibly, their parent edges).

To see what is the clade named "H7", for instance:
```{julia; eval=true; term=true; results="markup"}
BSc # this might be too big
showall(BSc)
BSc[:taxa][BSc[:H7]]
```
We can also get bootstrap values associated with edges, to describe the support that a given
hybrid clade has a given sister clade.
```{julia; eval=true; term=true; results="markup"}
BSe
```
Here, each row describes a pair of 2 clades: one being the hybrid, the other being its sister,
connected by a hybrid edge. The first rows corresponds to hybrid edges in the best network. Other
rows correspond to edges seen in bootstrap networks but not in the reference network.
```{julia; eval=true; term=true; results="markup"}
BSedgenum
```
lists all the hybrid edges in the best network, two for each hybrid node:
the major parent edge and then the minor parent edge.
In our case, there is only one reticulation, so only 2 hybrid edges.

We can plot the bootstrap values of the 2 hybrid edges in the best network:
```{julia; eval=false; results="markup"; label="boot_net_net"; fig_width=4; fig_height=4}
plot(net1, edgeLabel=BSe[[:edge,:BS_hybrid_edge]])
```
![boot_net_net](../assets/figures/bootstrap_boot_net_net_1.png)

This is showing the bootstrap support each hybrid edge: percentage of bootstrap trees with an
edge from the same sister clade to the same hybrid clade.
Alternatively, we could show the bootstrap support for the full reticulation relationships in
the network, one at each hybrid node (support for same hybrid with same sister clades).
Here, we find that A received gene flow from E (and is sister to B otherwise) in just 32%
of bootstrap networks. In another 1% bootstrap, A received gene flow from another source.
```{julia; eval=false; results="markup"; label="boot_net_ret"; fig_width=4; fig_height=4}
plot(net1, nodeLabel=BSn[[:hybridnode,:BS_hybrid_samesisters]])
```
![boot_net_ret](../assets/figures/bootstrap_boot_net_ret_1.png)

Below is example code to place tree edge support and hybrid edge support
on the same plot.

```julia
tmp = BSe[!isna(BSe[:edge]),[:edge,:BS_hybrid_edge]]
rename!(tmp, :BS_hybrid_edge, :proportion)
rename!(tmp, :edge, :edgeNumber)
tmp = vcat(BSe_tree, tmp)
plot(net1, edgeLabel=tmp, nodeLabel=BSn[[:hybridnode,:BS_hybrid_samesisters]])
```

### Who are the hybrids in bootstrap networks?

On a different plot, we can show the bootstrap support for hybrid clades,
first mapped to each node with positive hybrid support,
and then mapped on the parent edge of these nodes.
A is estimated as a hybrid in only 33% of our bootstrap networks.
In another 44%, it is the lineage to (E,O) that is estimated as
being of hybrid origin.
```{julia; eval=false; results="markup"; label="boot_net_hyb"; fig_width=4; fig_height=4}
plot(net1, nodeLabel=BSn[BSn[:BS_hybrid].>0, [:hybridnode,:BS_hybrid]])
plot(net1, edgeLabel=BSn[BSn[:BS_hybrid].>0, [:edge,:BS_hybrid]])
```
![boot_net_hyb 1](../assets/figures/bootstrap_boot_net_hyb_1.png)
![boot_net_hyb 2](../assets/figures/bootstrap_boot_net_hyb_2.png)

### Where is the origin of gene flow?

We can plot the support for the various placements
of the gene flow origin (minor sister clade),
first mapped to each node with positive support for being the origin of gene flow,
and then mapped along the parent edge of these nodes.
We filtered clades to show those with sister support > 5%:
```{julia; eval=false; results="markup"; label="boot_net_clade"; fig_width=4; fig_height=4}
plot(net1, nodeLabel=BSn[BSn[:BS_minor_sister].>5, [:node,:BS_minor_sister]])
plot(net1, edgeLabel=BSn[BSn[:BS_minor_sister].>5, [:edge,:BS_minor_sister]])
```
![boot_net_clade 1](../assets/figures/bootstrap_boot_net_clade_1.png)
![boot_net_clade 2](../assets/figures/bootstrap_boot_net_clade_2.png)

In our best network, the lineage to E is estimated as the origin
of gene flow, but this is recovered in only 41% of our bootstrap networks.
In another 49%, it is the lineage to A that is estimated as the *origin*
of gene flow: so gene flow is estimated in the opposite direction.
In this example, there is support for gene flow between (A,B) and (E,O),
but there is much uncertainty about its exact placement and about its direction.

Mapping the support for major sister clades might be interesting too:
```{julia; eval=false}
plot(net1, nodeLabel=BSn[BSn[:BS_major_sister].>5, [:node,:BS_major_sister]])
```

The estimated heritability γ on hybrid edges in the reference network, when present in a
bootstrap network, was also extracted:
```{julia; eval=true; term=true; results="markup"}
BSgam[1:3,:] # first 3 rows only
```
γ=0 values are for bootstrap replicates that did not have the edge in their network.
Basic summaries on γ values for a given edge, say the minor parent,
could be obtained like this:
```{julia; eval=false; term=true; results="markup"}
minimum(BSgam[:,2])
maximum(BSgam[:,2])
mean(BSgam[:,2])
std(BSgam[:,2])
```
